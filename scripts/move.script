//Page Load
DiagReturnPage=dp
click updateState,1
if(HandleCallback==1)
{
  click b[ReturnObj],0 //Run right click code as callback
  HandleCallback=0
  vars.returnVal.txt="null"
  ReturnVal=-1
}else
{
  prints "M605 S0",0 //Set manual mode
  printh 0A
  IdexMode=0
  prints "M114",0 //Report position
  printh 0A
}


//Close
prints "G0 F5000",0 //Reset speed
printh 0A
page ReturnPage

//Send Command
prints "G91",0
printh 0A
prints command.txt,0
printh 0A
prints "G90",0
printh 0A
prints "M114",0
printh 0A
//Send Command right click
prints command.txt,0
printh 0A
prints "M114",0
printh 0A

//bZHome
command.txt="G28 Z X"
click sendCommand,1


//Z
command.txt="G0 Z"
command.txt+=distance.txt
command.txt+=" F500"
click sendCommand,1



//Y Up
command.txt="G0 Y"
command.txt+=distance.txt
command.txt+=" F"
command.txt+=speed.txt
click sendCommand,1

//Y down
command.txt="G0 Y-"
command.txt+=distance.txt
command.txt+=" F"
command.txt+=speed.txt
click sendCommand,1


//--------------------------
//Update State
//--------------------------
tX.txt="X:"
tX.txt+=printer.x.txt
tY.txt="Y:"
tY.txt+=printer.y.txt
tZ.txt="Z:"
tZ.txt+=printer.z.txt

if(busy==1) //Busy
{
  if(tBusy.aph==127) //Flash
  {
    tBusy.aph=0
  }else
  {
    tBusy.aph=127
  }
}else
{
  tBusy.aph=0
}
if(Tool==0) //Toolhead
{
  bSwap.picc=20
}else
{
  bSwap.picc=21
}

for(j=0;j<=35;j++) //Button Groups
{
  if(b[j].type==98) //If text box
  {
    if(b[j].txt_maxl==5) //Distance boxes are set with a maxl of 5
    {
      if(distance.txt==b[j].txt)
      {
        b[j].picc=19
        b[j].pco=0
      }else
      {
        b[j].picc=20
        b[j].pco=65535
      }
    }
    if(b[j].txt_maxl==6) //Speed boxes are set with a maxl of 6
    {
      if(speed.txt==b[j].txt)
      {
        b[j].picc=19
        b[j].pco=0
      }else
      {
        b[j].picc=20
        b[j].pco=65535
      }
    }
  }
}

//tX
ReturnObj=29
numpad.tCaption.txt="X Location"
vars.returnVal.txt=""
page numpad

//tX callback
if(vars.returnVal.txt!="null")
{
  command.txt="G0 X"
  command.txt+=vars.returnVal.txt
  command.txt+=" F"
  command.txt+=speed.txt
  click sendCommand,0 //0 does not prepend G91
}

//tY
ReturnObj=36
numpad.tCaption.txt="Y Location"
vars.returnVal.txt=""
page numpad

//tY callback
if(vars.returnVal.txt!="null")
{
  command.txt="G0 Y"
  command.txt+=vars.returnVal.txt
  command.txt+=" F"
  command.txt+=speed.txt
  click sendCommand,0 //0 does not prepend G91
}

//tZ
ReturnObj=37
numpad.tCaption.txt="Z Location"
vars.returnVal.txt=""
page numpad

//tZ callback
if(vars.returnVal.txt!="null")
{
  command.txt="G0 Z"
  command.txt+=vars.returnVal.txt
  command.txt+=" F500"
  click sendCommand,0 //0 does not prepend G91
}