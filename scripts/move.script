///////////////////////////////////////////////////////////////////////////////
/// @fn Pre-initialize
/// @brief Page Initialization
///////////////////////////////////////////////////////////////////////////////
parse.callback.txt="null"
parse.callbackReturn.txt="null"
click luxView,1
click luxAnimate,1
DiagReturnPage=dp
if(HandleCallback==1)
{
  if(ReturnObj==0) //Direct to custom controller callback
  {
    click luxController,0
  }else
  {
    click b[ReturnObj],0
  }
  ReturnVal=-1
  vars.returnVal.txt="null"
  HandleCallback=0
}else
{
  commands.queue.txt+="M605 S0\r" //Set manual idex mode
  IdexMode=0
  commands.queue.txt+="M114\r"
  commands.queue.txt+="G91\r"
  click luxSend,0
}
click luxController,1

///////////////////////////////////////////////////////////////////////////////
/// @fn bClose
/// @brief Close button
///////////////////////////////////////////////////////////////////////////////
commands.queue.txt+="G0 F5000\r" //Reset speed
commands.queue.txt+="G90\r" //Back to absolution positioning
click luxSend,0
page ReturnPage

///////////////////////////////////////////////////////////////////////////////
/// @fn luxController,1
/// @brief Program loop
///////////////////////////////////////////////////////////////////////////////
//Empty

///////////////////////////////////////////////////////////////////////////////
/// @fn luxView,1
/// @brief Updates states for controls
///////////////////////////////////////////////////////////////////////////////
tX.txt="X:"
tX.txt+=printer.x.txt
tY.txt="Y:"
tY.txt+=printer.y.txt
tZ.txt="Z:"
tZ.txt+=printer.z.txt
for(j=0;j<=35;j++) //Button Groups
{
  if(b[j].type==98) //If text box
  {
    if(b[j].txt_maxl==5) //Distance boxes are set with a maxl of 5
    {
      if(distance.txt==b[j].txt)
      {
        b[j].picc=b[j].picc2
        b[j].pco=0
      }else
      {
        b[j].picc=b[j].picc2+1
        b[j].pco=65535
      }
    }
    if(b[j].txt_maxl==6) //Speed boxes are set with a maxl of 6
    {
      if(speed.txt==b[j].txt)
      {
        b[j].picc=b[j].picc2
        b[j].pco=0
      }else
      {
        b[j].picc=b[j].picc2+1
        b[j].pco=65535
      }
    }
  }
}

///////////////////////////////////////////////////////////////////////////////
/// @fn luxPageAnimate
/// @brief Animation Loop
///////////////////////////////////////////////////////////////////////////////
if(busy==1) //Busy
{
  if(Frame==0) //Flash
  {
    tBusy.aph=0
  }else
  {
    tBusy.aph=127
  }
}else
{
  tBusy.aph=0
}
bSwap.picc=bSwap.picc2+1
bSwap.picc+=Tool

///////////////////////////////////////////////////////////////////////////////
/// @fn sendMovement
/// @brief Sends movement command to printer
///////////////////////////////////////////////////////////////////////////////
commands.command.txt+=command.txt
commands.command.txt+="\r"
commands.command.txt+="M114\r"
click luxSend,1

///////////////////////////////////////////////////////////////////////////////
//bZHome
command.txt="G28 Z X"
click sendMovement,1

//bXYHome
command.txt="G28 X Y"
click sendMovement,1

//bZDown
command.txt="G0 Z-"
command.txt+=distance.txt
command.txt+=" F500"
click sendMovement,1

//bZUp
command.txt="G0 Z"
command.txt+=distance.txt
command.txt+=" F500"
click sendMovement,1

//bYUp
command.txt="G0 Y"
command.txt+=distance.txt
command.txt+=" F"
command.txt+=speed.txt
click sendMovement,1

//bYDown
command.txt="G0 Y-"
command.txt+=distance.txt
command.txt+=" F"
command.txt+=speed.txt
click sendMovement,1

//bXUp
command.txt="G0 X"
command.txt+=distance.txt
command.txt+=" F"
command.txt+=speed.txt
click sendMovement,1

//bXDown
command.txt="G0 X-"
command.txt+=distance.txt
command.txt+=" F"
command.txt+=speed.txt
click sendMovement,1

//tX
ReturnObj=!!
numpad.tCaption.txt="X Location"
vars.returnVal.txt=""
page numpad
//tX callback
if(vars.returnVal.txt!="null")
{
  commands.command.txt="G0 X"
  commands.command.txt+=vars.returnVal.txt
  commands.command.txt+=" F"
  commands.command.txt+=speed.txt
  click luxSend,1 //Send immediatly
}

//tY
ReturnObj=!!
numpad.tCaption.txt="Y Location"
vars.returnVal.txt=""
page numpad
//tY callback
if(vars.returnVal.txt!="null")
{
  commands.command.txt="G0 Y"
  commands.command.txt+=vars.returnVal.txt
  commands.command.txt+=" F"
  commands.command.txt+=speed.txt
  click luxSend,1 //Send immediatly
}

//tZ
ReturnObj=!!
numpad.tCaption.txt="Z Location"
vars.returnVal.txt=""
page numpad
//tZ callback
if(vars.returnVal.txt!="null")
{
  commands.command.txt="G0 Z"
  commands.command.txt+=vars.returnVal.txt
  commands.command.txt+=" F500"
  click luxSend,1 //Send immediatly
}

//Distances
distance.txt=bDistance1.txt
click sendMovement,1

//Speeds
speed.txt=bSpeed1.txt
click sendMovement,1