///////////////////////////////////////////////////////////////////////////////
/// @fn luxController,1
/// @brief Program loop
///////////////////////////////////////////////////////////////////////////////
if(state.txt=="null")
{
  state.txt="connecting"   
}
if(state.txt=="connecting")
{
  if(buffer.en!=1)
  {
    click startConnect,1
  }else
  {
    state.txt="setting=defaults"
  }
}
if(state.txt=="setting-defaults")
{
  click setDefaults,1
  state.txt="connected"
}
if(state.txt=="connected")
{
  if(tmFinished.en==0)
  {
    tmFinished.en=1  
  }
  
  if(ok==1&&QueueBlocks==0)
  {
    if(luxAnimate.en==0)
    {
      tmFinished.en=1      
    }
  }  
}

///////////////////////////////////////////////////////////////////////////////
/// @fn startConnect,1
/// @brief Connect trying different baud rates automatically
///////////////////////////////////////////////////////////////////////////////
if(tries.val<9) //Try connecting 10 times
{
  if(usize<=0) //If the buffer is empty
  {
    if(k==0)
    {
      baud=115200
      Baud=115200
    }
    if(k==1)
    {
      baud=9600
      Baud=9600
    }
    if(k==2)
    {
      baud=9600
      Baud=9600
    }
    if(k==3)
    {
      baud=250000
      Baud=250000
    }
    if(k>3)
    {
      k=0 //Start over and try again
    }else
    {
      k++ //Try the next baud value on the next iteration
    }
    covx Baud,vars.s.txt,0,0  //Output status
    vars.status.txt="Trying baudrate "
    vars.status.txt+=vars.s.txt
    commands.command.txt="M155 S2"  //Try and trigger a response
    click luxSend,1
    tries.val++
  }else
  {
    buffer.en=1
    vars.status.txt="Connected at "
    covx Baud,vars.s.txt,0,0
    vars.status.txt+=vars.s.txt
    state.txt="setting-defaults"
  }
}else
{
  state.txt="timeout"
  vars.status.txt="Could not connect.  Unplug USB when shutting down."
  click luxView,1
}


///////////////////////////////////////////////////////////////////////////////
/// @fn setDefaults,1
/// @brief Report printer settings and set printer defaults
///////////////////////////////////////////////////////////////////////////////
commands.command.txt="" //Clear connection preamble sent by tft
click luxSend,1
commands.command.txt="M115\r" //Get Marlin version
commands.command.txt+="M503\r" //Get settings
commands.command.txt+="M114\r" //Get position
commands.command.txt+="M155 S5\r" //Temp auto report
// commands.command.txt+="G28 X\r"
//commands.command.txt+="M605 S1\r" //Dual Nozzle Mode Auto
//commands.command.txt+="T0\r" //Select T0
commands.command.txt+="M220\rM221\r" //Get flow and speed
click luxSend,1


///////////////////////////////////////////////////////////////////////////////
/// @fn luxView,1
/// @brief Updates states for controls
///////////////////////////////////////////////////////////////////////////////
tStatus.txt=vars.status.txt
if(state.txt=="null")
{
  tFirmwareName.aph=0
  tMachine.aph=0
}
if(state.txt=="timeout")
{
  tsw pBanner,0
  pBanner.aph=0
  pState.aph=0
  setlayer pState,255
}
if(state.txt=="connected")
{
  tFirmwareName.aph=127
  tMachine.aph=127
  tLuxVersion.aph=0
  tStatus.aph=0
  tFirmwareName.txt=printer.firmwareName.txt
  tMachine.txt=printer.machineType.txt
}

///////////////////////////////////////////////////////////////////////////////
/// @fn luxAnimate
/// @brief Animation Loop
///////////////////////////////////////////////////////////////////////////////
if(pBanner.y>0)
{
  pBanner.y-=4
}else
{
  luxAnimate.en=0
}

///////////////////////////////////////////////////////////////////////////////
/// @fn tmFinished
/// @brief Exit page after pausing to show version number
///////////////////////////////////////////////////////////////////////////////
page MAIN

///////////////////////////////////////////////////////////////////////////////
/// @fn luxRun
/// @breif Main Program Loop
///////////////////////////////////////////////////////////////////////////////
click luxRead,1
click luxView,1
click luxController,1
click luxSend,0

///////////////////////////////////////////////////////////////////////////////
/// @fn Pre-initialize
/// @brief Page Initialization
///////////////////////////////////////////////////////////////////////////////
click luxView,1