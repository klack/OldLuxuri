///////////////////////////////////////////////////////////////////////////////
/// @fn controller,1
///
/// @brief Program loop
/// @brief state.txts: null, connecting, setting-defaults, connected, timeout
///////////////////////////////////////////////////////////////////////////////
if(state.txt=="null")
{
  state.txt="connecting"   
}
if(state.txt=="connecting")
{
  click startConnect,1
}
if(state.txt=="setting-defaults")
{
  click setDefaults,1
}
if(state.txt=="connected")
{
  if(ok==1&&animate.en==0)
  {
    delay=3000
    page main
  }
}
if(state.txt=="timeout")
{
  dim=50
}


///////////////////////////////////////////////////////////////////////////////
/// @fn startConnect,1
///
/// @brief Connect trying different baud rates automatically
///////////////////////////////////////////////////////////////////////////////
if(j<200) //Try connecting 10 times
{
  if(usize<=0) //If the buffer is empty
  {
    if(k==0)
    {
      baud=115200
      Baud=115200
    }
    if(k==1)
    {
      baud=9600
      Baud=9600
    }
    if(k==2)
    {
      baud=9600
      Baud=9600
    }      
    if(k==3)
    {
      baud=250000
      Baud=250000
    }
    if(k>1)
    {
      k=0 //Start over and try again
    }else
    {
      k++ //Try the next baud value on the next iteration
    }
    covx Baud,vars.s.txt,0,0  //Output status
    vars.status.txt="Trying baudrate "
    vars.status.txt+=vars.s.txt
    commands.command.txt="M155 S2"  //Try and trigger a response
    click sendCommand,1
    j++
  }else
  { 
    buffer.en=1
    vars.status.txt="Connected at "
    covx Baud,vars.s.txt,0,0
    vars.status.txt+=vars.s.txt
    state.txt="setting-defaults"
  }
}else
{
  state.txt="timeout"
  click updateView,1
  vars.status.txt="Unplug USB to complete shutdown"
}

///////////////////////////////////////////////////////////////////////////////
/// @fn setDefaults,1
///
/// @brief Report printer settings and set printer defaults
///////////////////////////////////////////////////////////////////////////////
commands.command.txt="" //Clear connection preamble sent by tft
click sendCommand,1
commands.queue.txt="M115" //Get Marlin version
commands.queue.txt+="M503" //Get settings
commands.queue.txt+="M114" //Get position
commands.queue.txt+="M155 S2" //Temp auto report
commands.queue.txt+="M605 S1" //Dual Nozzle Mode Auto
commands.queue.txt+="T0" //Select T0
click sendCommand,0
state.txt="connected"

///////////////////////////////////////////////////////////////////////////////
/// @fn buffer
/// @brief Read and parse buffer on 50ms timer
///////////////////////////////////////////////////////////////////////////////
click readBuffer,1

///////////////////////////////////////////////////////////////////////////////
/// @fn animate
/// @brief Animate banners
///////////////////////////////////////////////////////////////////////////////
if(pBanner.y>0)
{
  pBanner.y-=4
}else
{
  animate.en=0
}


///////////////////////////////////////////////////////////////////////////////
/// @fn run
/// @brief Program Loop on 1000ms timer
///////////////////////////////////////////////////////////////////////////////
click updateView,1
click controller,1

///////////////////////////////////////////////////////////////////////////////
/// @fn updateView,1
/// @breif Handle display state
///////////////////////////////////////////////////////////////////////////////
tStatus.txt=vars.status.txt
if(state.txt=="null")
{
  tVersion.aph=0
  tMachine.aph=0
}
if(state.txt=="timeout")
{
  dim=100
  pState.aph=0
  pBanner.aph=0
}
if(state.txt=="connected")
{
  tVersion.aph=127
  tMachine.aph=127
  tStatus.aph=0
  tVersion.txt=version.txt
  tMachine.txt=machine.txt
}

///////////////////////////////////////////////////////////////////////////////
/// @fn Pre-initialize
///////////////////////////////////////////////////////////////////////////////
click updateView,1