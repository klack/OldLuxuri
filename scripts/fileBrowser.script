///////////////////////////////////////////////////////////////////////////////
/// @fn controller,1
///
/// @brief Program loop
/// @brief state.txts: null, getting-files, ready, no-sd
///////////////////////////////////////////////////////////////////////////////
if(state.txt=="null")
{
  state.txt="getting-files"   
}
if(state.txt=="getting-files")
{
  //On Load
  commands.queue.txt+="M155 S0" //Select T0
  commands.queue.txt+="M22"
  commands.queue.txt+="M21"
  commands.queue.txt+="M20"
  vars.status.txt="Getting Files"
  click sendCommand,0
}
if(state.txt=="ready")
{

}
if(state.txt=="no-sd")
{

}

//---------------------------------------------
//Update State
//---------------------------------------------
if(state.txt=="null")
{

}
if(state.txt=="getting-files")
{
 
}
if(state.txt=="ready")
{
  
}
if(state.txt=="no-sd")
{
  
}
if(page.val==0)
{
  setlayer bUp,0
}else
{
  setlayer bUp,254
}
spstr tFiles.txt,vars.s.txt,">",page.val+1*5 //Check for last page
if(vars.s.txt=="")
{
  setlayer bDown,0
}else
{
  setlayer bDown,254
}
strlen valueLFile.txt,j
if(j>0)
{
  spstr valueLFile.txt,f0.txt,">",0
  spstr valueLFile.txt,f1.txt,">",1
  spstr valueLFile.txt,f2.txt,">",2
  spstr valueLFile.txt,f3.txt,">",3
  spstr valueLFile.txt,f4.txt,">",4
  for(j=0;j<=36;j++) //Button Group
  {
    if(b[j].type==116&&b[j].txt_maxl==254) //If filetext box
    {
      if(b[j].txt==selectedFLong.txt)
      {
        b[j].pco=65504
      }else
      {
        b[j].pco=65535
      }
    }
  }
}
if(selectedFile.txt=="")
{
  tsw bPrint,0
  bPrint.aph=0
}else
{
  tsw bPrint,1
  bPrint.aph=127
}

  
//---------------------------------------------
//Get Long Files
//---------------------------------------------
keyLFile.txt=""
valueLFile.txt=""
lFilesReq.val=0
lFilesRecieved.val=0
gettingLongF.val=1
j=page.val*5
for(k=0;k<=4;k++)
{
  spstr tFiles.txt,vars.s.txt,">",j+k
  if(vars.s.txt!="")
  {
    keyLFile.txt+=vars.s.txt //Store key
    keyLFile.txt+=">"
    commands.queue.txt+="M33 "
    commands.queue.txt+=vars.s.txt
    commands.queue.txt+="\r"
    lFilesReq.val+=1
  }
}
click sendCommand,0

//bPrint
print.fileName.txt=selectedFile.txt

//fn click
selectedIndex.val=0
click selectFile,1
click updateState,1

//selectFile
spstr keyLFile.txt,vars.s.txt,">",selectedIndex.val
selectedFile.txt=vars.s.txt
spstr valueLFile.txt,vars.s.txt,">",selectedIndex.val
selectedFLong.txt=vars.s.txt
print.tFileName.txt=selectedFLong.txt
click updateState,1





//On Exit
printer.command.txt="M155 S2" //Temp auto report on
printh 0A

//bUp
if(page.val>0)
{
  selectedFile.txt=""
  selectedIndex.val=-1
  page.val-=1
  click getLongfiles,1
}
click selectFile,1

//bDown
selectedFile.txt=""
selectedIndex.val=-1
page.val+=1
click getLongfiles,1
click selectFile,1

//---------------------------------------------
//parseFiles
//---------------------------------------------
parse.lineMatch.txt="End file list"
if(parse.buffer.txt==parse.lineMatch.txt)
{
  gettingFiles.val=0 //Getting file state is off, do not listen for filenames
  click getLongfiles,1
}
if(gettingFiles.val==1) //Get files
{
  spstr parse.buffer.txt,vars.s.txt," ",0
  strlen vars.s.txt,j
  if(j>0)
  {
    substr parse.buffer.txt,vars.s2.txt,0,1
    if(vars.s2.txt!="/") //Skip directories
    {
      tFiles.txt+=vars.s.txt  //Got a file
      tFiles.txt+=">" //Delimeter to be used later
    }
  }
}
parse.lineMatch.txt="Begin file list"
if(parse.buffer.txt==parse.lineMatch.txt)
{
  tFiles.txt="" //Clear existing file list
  page.val=0
  gettingFiles.val=1   //Getting file state is on, next iteration will process for file name
}


//---------------------------------------------
//parseLongF
//---------------------------------------------
if(gettingLongF.val==1)
{
  parse.lineMatch.txt="/"
  strlen parse.lineMatch.txt,j
  substr parse.buffer.txt,parse.eval.txt,0,j
  if(parse.eval.txt==parse.lineMatch.txt)
  {
    spstr parse.buffer.txt,vars.s.txt,"/",1 //Grab after the slash
    valueLFile.txt+=vars.s.txt
    valueLFile.txt+=">"
    lFilesRecieved.val+=1
  }
  if(lFilesRecieved.val==lFilesReq.val)
  {
    gettingLongF.val=0
    click updateState,1
  }
}