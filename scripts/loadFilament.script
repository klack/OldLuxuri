///////////////////////////////////////////////////////////////////////////////
/// @fn luxRunOnce
/// @brief Warm up extruders
///////////////////////////////////////////////////////////////////////////////
commands.queue.txt+="M605 S0\r" //Set manual mode
click luxSend,0

///////////////////////////////////////////////////////////////////////////////
/// @fn luxInitialize 0
/// @brief Should run on any close button
///////////////////////////////////////////////////////////////////////////////
commands.queue.txt+="M605 S1\r" //Set back to auto mode
click luxSend,0
page ReturnPage

///////////////////////////////////////////////////////////////////////////////
/// @fn luxController
/// @brief Main program loop
///////////////////////////////////////////////////////////////////////////////
if(state.txt=="init")
{
  //Handle temperature already set
  if(printer.T0SetTemp.val>0&&printer.T0SetTemp.val>minTemp.val)
  {
    T0Target.val=printer.T0SetTemp.val
  }
  if(printer.T1SetTemp.val>0&&printer.T1SetTemp.val>minTemp.val)
  {
    T1Target.val=printer.T1SetTemp.val
  }
}

if(printer.T0SetTemp.val>0&&T0State.txt!="ready")
{
  T0State.txt="heating"
}else
{
  T0State.txt="off"
}
if(printer.T1SetTemp.val>0&&T1State.txt!="ready")
{
  T1State.txt="heating" 
}else
{
  T1State.txt="off"  
}

if(printer.T0Temp.val>=T0Target.val)
{
  T0State.txt="ready"
}
if(printer.T1Temp.val>=T1Target.val)
{
  T1State.txt="ready"
}

if(T0State.txt=="off"&&T1State.txt=="off")
{
  state.txt="choose"
}
if(T0State.txt=="heating"||T1State.txt=="heating")
{
  state.txt="heating"
}
if(T0State.txt=="ready"||T1State.txt=="ready")
{
  state.txt="ready"
}

///////////////////////////////////////////////////////////////////////////////
/// @fn luxView
/// @brief Updates states for controls
///////////////////////////////////////////////////////////////////////////////
if(T0State.txt=="ready") //Show hide buttons
{
  setlayer bT0Out,255
  setlayer bT0In,255
  setlayer bHeatT0,1
}else
{
  setlayer bT0Out,1
  setlayer bT0In,1
  setlayer bHeatT0,255
}
if(T1State.txt=="ready")
{
  setlayer bT1Out,255
  setlayer bT1In,255
  setlayer bHeatT1,1
}else
{
  setlayer bT1Out,1
  setlayer bT1In,1
  setlayer bHeatT1,255
}
if(T0State.txt=="heating") //Heating pics
{
  bHeatT0.picc=bHeatT0.picc2+3
}else
{
  bHeatT0.picc=bHeatT0.picc2+1
}
if(T1State.txt=="heating")
{
  bHeatT1.picc=bHeatT1.picc2+3
}else
{
  bHeatT1.picc=bHeatT1.picc2+1
}
if(state.txt=="choose") //Page state
{
  pIcon.picc=pState.picc-1
  tMessage2.aph=127
  tMessage.txt="Heat"
  tMessage2.txt="Extruders"
}
if(state.txt=="heating")
{
  pIcon.picc=pState.picc+1
  tMessage2.aph=127
  tMessage.txt="Heating"
  tMessage2.txt="Please Wait..."
}
if(state.txt=="ready")
{
  pIcon.picc=pState.picc+3
  tMessage2.aph=0
  tMessage.txt="Ready"
}
covx printer.T0Temp.val,vars.s.txt,0,0 //Temperature display
tT0Temp.txt=vars.s.txt
covx printer.T1Temp.val,vars.s.txt,0,0
tT1Temp.txt=vars.s.txt

///////////////////////////////////////////////////////////////////////////////
/// @fn luxPageAnimate
/// @brief Page Animations
///////////////////////////////////////////////////////////////////////////////
if(T0State.txt=="heating")
{
  if(Frame==0)
  {
    bHeatT0.aph=0
  }else
  {
    bHeatT0.aph=127
  }
}else
{
  bHeatT0.aph=127
}
if(T1State.txt=="heating")
{
  if(Frame==0)
  {
    bHeatT1.aph=0
  }else
  {
    bHeatT1.aph=127
  }
}else
{
  bHeatT1.aph=127
}

///////////////////////////////////////////////////////////////////////////////
//bHeatT0
covx T0Target.val,vars.s.txt,0,0 //Heat to load temp
commands.queue.txt+="M104 T0 S"
commands.queue.txt+=vars.s.txt
commands.queue.txt+="\r"
click luxSend,0

//bHeatT1
covx T1Target.val,vars.s.txt,0,0 //Heat to load temp
commands.queue.txt+="M104 T1 S"
commands.queue.txt+=vars.s.txt
commands.queue.txt+="\r"
click luxSend,0

//Send Command
commands.queue.txt+="T"
covx commandTool.val,vars.s.txt,0,0
commands.queue.txt+=vars.s.txt
commands.queue.txt+="\rG92 E0\r" //Set extruder pos to 0
commands.queue.txt+="G1 E"
covx commandLength.val,vars.s2.txt,0,0
commands.queue.txt+=vars.s2.txt
commands.queue.txt+=" F500\r"
commands.queue.txt+="G92 E0\r"
click luxSend,0

//bT0Out
commandTool.val=0
commandLength.val=0-loadLength.val //Change to negative number
click sendCommand,1

//bT0In
commandTool.val=0
commandLength.val=loadLength.val //Change to negative number
click sendCommand,1

//bT1Out
commandTool.val=1
commandLength.val=0-loadLength.val //Change to negative number
click sendCommand,1

//bT1In
commandTool.val=1
commandLength.val=loadLength.val //Change to negative number
click sendCommand,1