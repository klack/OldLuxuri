///////////////////////////////////////////////////////////////////////////////
/// @fn luxPause 1
/// @brief Pause the print
///////////////////////////////////////////////////////////////////////////////
printer.x.txt="?" //Clear out so it can be caught later
printer.y.txt="?"
printer.z.txt="?"
commands.queue.txt+="M25\r" 
commands.queue.txt+="M400\r" //Trigger busy until planner is finished
commands.queue.txt+="M114\r"
click luxSend,0

///////////////////////////////////////////////////////////////////////////////
/// @fn luxPause 0
/// @fn Goes to paused locaiton
///////////////////////////////////////////////////////////////////////////////
printer.storedX.txt=printer.x.txt
printer.storedY.txt=printer.y.txt
printer.storedZ.txt=printer.z.txt
commands.queue.txt+="G0 X"
commands.queue.txt+=printer.pausedX.txt
commands.queue.txt+=" Y"
commands.queue.txt+=printer.pausedY.txt
commands.queue.txt+="\r"
click luxSend,0

///////////////////////////////////////////////////////////////////////////////
/// @fn luxResume 1
/// @brief Go back to old coordinates and resume the print
///////////////////////////////////////////////////////////////////////////////
commands.queue.txt+="G0 X"
commands.queue.txt+=printer.storedX.txt
commands.queue.txt+=" Y"
commands.queue.txt+=printer.storedY.txt
commands.queue.txt+=" Z"
commands.queue.txt+=printer.storedZ.txt
commands.queue.txt+="\r"
commands.queue.txt+="M24\r"
printer.sdPrinting.val=1
click luxSend,0

///////////////////////////////////////////////////////////////////////////////
/// @fn luxFlow 1
/// @brief Get Flow
///////////////////////////////////////////////////////////////////////////////
commands.queue.txt+="M221\r"
click luxSend,0

///////////////////////////////////////////////////////////////////////////////
/// @fn luxFlow 0
/// @brief Set Flow
///////////////////////////////////////////////////////////////////////////////
commands.queue.txt+="M221 S"
commands.queue.txt+=vars.returnVal.txt
commands.queue.txt+="\rM221\r"
click luxSend,0

///////////////////////////////////////////////////////////////////////////////
/// @fn luxSpeed 1
/// @brief Get Speed
///////////////////////////////////////////////////////////////////////////////
commands.queue.txt+="M220\r"
click luxSend,0

///////////////////////////////////////////////////////////////////////////////
/// @fn luxSpeed 0
/// @brief Set Speed
///////////////////////////////////////////////////////////////////////////////
commands.queue.txt+="M220 S"
commands.queue.txt+=vars.returnVal.txt
commands.queue.txt+="\rM220\r"
click luxSend,0

///////////////////////////////////////////////////////////////////////////////
/// @fn luxStopPrint 1
/// @brief Return printer to default settings
///////////////////////////////////////////////////////////////////////////////
commands.queue.txt="M27 S0\rM104 T0 S0\rM104 T1 S0\rM140 S0\rM106 S0\rG0 X Y\rM605 S1\rT0\rM220 S100\rM221 S100\rM220\rM221\r"
click luxSend,0

///////////////////////////////////////////////////////////////////////////////
/// @fn luxStopPrint 0
/// @brief Break through a busy or waiting for response status
///////////////////////////////////////////////////////////////////////////////
commands.queue.txt=""
for(l=0;l<5;l++)
{
  ok=0
  while(ok==0)
  {
    prints "M108",0
    printh 0A
    prints "M524",0 //Stop print
    printh 0A 
    delay=500
    click luxRead,1
  }
}


///////////////////////////////////////////////////////////////////////////////
/// @fn luxPrintTime 1
///////////////////////////////////////////////////////////////////////////////
strlen commands.queue.txt,j
if(ok==1&&j==0) //Only get time if an ok was recieved
{
  commands.queue.txt+="M31\r"
  TimeCount+=1
}

///////////////////////////////////////////////////////////////////////////////
/// @fn luxCheckSleep
///////////////////////////////////////////////////////////////////////////////
if(printer.sdPrinting.val==0) //Handle a finished print while LCD was asleep
{
  printer.printState.txt="finishing"
  commands.queue.txt+="" //Clear invalid commands
  click luxView,1
  click luxSend,0
  strlen commands.queue.txt,c //See if there are commands in the queue
  if(ok==1&&c==0)
  {
    commands.queue.txt+="M31\r" //Get print time
  }
}

///////////////////////////////////////////////////////////////////////////////
/// @fn luxStartPrint
///////////////////////////////////////////////////////////////////////////////
commands.queue.txt+="M114\r" //Get location
commands.queue.txt+="M220\rM221\r" //Get flow / speed
commands.queue.txt+="M27 S5\r"  //Report SD Status
commands.queue.txt+="M23 "
commands.queue.txt+=fileName.txt
commands.queue.txt+="\rM24\r"
printer.printTime.txt=""
printer.currentByte.val=0
printer.totalBytes.val=0
click luxSend,0

///////////////////////////////////////////////////////////////////////////////
/// @fn luxGetPosition
///////////////////////////////////////////////////////////////////////////////
commands.queue.txt+="M114"
click luxSend,0