//--------------------
//Update State
//--------------------
//Update T0
covx printer.T0Temp.val,vars.s.txt,0,0
tT0Temp.txt=vars.s.txt
tT0Temp.txt+="/"
covx printer.T0SetTemp.val,vars.s.txt,0,0
tT0Temp.txt+=vars.s.txt
//Update T1
covx printer.T1Temp.val,vars.s.txt,0,0
tT1Temp.txt=vars.s.txt
tT1Temp.txt+="/"
covx printer.T1SetTemp.val,vars.s.txt,0,0
tT1Temp.txt+=vars.s.txt
//Update Bed
tBedTemp.txt="Bed: "
covx printer.bedTemp.val,vars.s.txt,0,0
tBedTemp.txt+=vars.s.txt
tBedTemp.txt+="/"
covx printer.setBedTemp.val,vars.s.txt,0,0
tBedTemp.txt+=vars.s.txt

//Swap States
for(j=0;j<=32;j++) //Button Groups
{
  if(b[j].type==98) //If text box
  {
    if(b[j].txt_maxl==4) //T0 boxes are set with a maxl of 5
    {
      if(T0Mat.txt==b[j].txt)
      {
        b[j].picc=22
        b[j].pco=0
      }else
      {
        b[j].picc=3
        b[j].pco=65535
      }
    }
    if(b[j].txt_maxl==5) //T1 boxes are set with a maxl of 6
    {
      if(T1Mat.txt==b[j].txt)
      {
        b[j].picc=22
        b[j].pco=0
      }else
      {
        b[j].picc=3
        b[j].pco=65535
      }
    }
  }
}

j=0
k=0
l=0
//Check T0Temp
if(printer.T0Temp.val>=preheatT0Temp.val)
{
  j=1
}
//Check T1Temp
if(printer.T1Temp.val>=preheatT1Temp.val)
{
  k=1
}
//Check Bed Temp
if(printer.bedTemp.val>=printer.setBedTemp.val)
{
  l=1
}
State=j+k+l
//Set States
if(State==3)
{
  //Temperature Reached
  state.picc=24 //Change message to ready
  tMessage.txt="Ready"
  icon.picc=24
  tMessage2.aph=0
  ready.val=1
}else
{
  if(ready.val==0)
  {
    if(preheatT0Temp.val>0||preheatT1Temp.val>0)
    {
      //Heating
      tMessage.txt="Heating"
      tMessage2.txt="Please wait..."
      icon.picc=23
      if(tMessage2.aph==127)
      {
        tMessage2.aph=0
      }else
      {
        tMessage2.aph=127
      }    
    }else
    {
      //Choose temp
      tMessage.txt="Choose"
      tMessage2.txt="Materials"
      tMessage2.aph=127
      icon.picc=2
    }
  }
}
//--------------------
//End UpdateState
//--------------------



//Send Command
prints "M104 T0 S",0 //Set T0
covx preheatT0Temp.val,vars.s.txt,0,0
prints vars.s.txt,0
printh 0A
prints "M104 T1 S",0 //Set T1
covx preheatT1Temp.val,vars.s.txt,0,0
prints vars.s.txt,0
printh 0A
if(preheatT0Bed.val>preheatT1Bed.val) //Set the higher bed temp of the two
{
  j=preheatT0Bed.val
}else
{
  j=preheatT1Bed.val
}
prints "M140 S",0
covx j,vars.s.txt,0,0
prints vars.s.txt,0
printh 0A
ready.val=0
click updateState,1




//bT0None
preheatT0Temp.val=0
T0Mat.txt="None"
preheatT0Bed.val=0
click sendCommand,1

//bT0PLA
preheatT0Temp.val=200
T0Mat.txt="PLA"
preheatT0Bed.val=60
click sendCommand,1

//bT0PETG
preheatT0Temp.val=230
T0Mat.txt="PETG"
preheatT0Bed.val=70
click sendCommand,1

//bT0TPU
preheatT0Temp.val=220
T0Mat.txt="TPU"
preheatT0Bed.val=40
click sendCommand,1

//bT0PVA
preheatT0Temp.val=220
T0Mat.txt="PVA"
preheatT0Bed.val=60
click sendCommand,1




//bT1None
preheatT1Temp.val=0
T1Mat.txt="None"
preheatT1Bed.val=0
click sendCommand,1

//bT1PLA
preheatT1Temp.val=200
T1Mat.txt="PLA"
preheatT1Bed.val=60
click sendCommand,1

//bT1PETG
preheatT1Temp.val=230
T1Mat.txt="PETG"
preheatT1Bed.val=70
click sendCommand,1

//bT1TPU
preheatT1Temp.val=220
T1Mat.txt="TPU"
preheatT1Bed.val=40
click sendCommand,1

//bT1PVA
preheatT1Temp.val=220
T1Mat.txt="PVA"
preheatT1Bed.val=60
click sendCommand,1