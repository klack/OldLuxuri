///////////////////////////////////////////////////////////////////////////////
/// @fn luxController 1
/// @brief Program loop
///////////////////////////////////////////////////////////////////////////////
if(state.txt=="null")
{
  diagPrompt.tMessage.txt="Perform PID Autotune?\rThis will take some time."
  ReturnVal=1
  ReturnObj=0
  page diagPrompt
}
if(state.txt=="init")
{
  thsp=0 //Disable sleep
  click checkHotbed,1
  click checkHotends,1
  if(state.txt=="init") //State has was not changed
  {
    state.txt="tuning"
    click luxView,1
  }
}
if(state.txt=="cooling")
{
  if(printer.T0Temp.val<40&&printer.T1Temp.val<40)
  {
    state.txt="tuning"
    click luxView,1
  }  
}
if(state.txt=="tuning")
{
  oProgress.val=cycle.val*100/totalCycles.val
  if(oProgress.val==100)
  {
    state.txt="complete"
    click luxView,1
    click finish,1
  }
}

///////////////////////////////////////////////////////////////////////////////
/// @fn luxController 0
/// @brief Custom on page callback handling
///////////////////////////////////////////////////////////////////////////////
if(ReturnVal==1) //Handle start confirmation diag
{
  state.txt="init"
}else
{
  page ReturnPage
}

///////////////////////////////////////////////////////////////////////////////
/// @fn luxInit 0
/// @brief Call when perminently exiting the page
///////////////////////////////////////////////////////////////////////////////
if(state.txt=="tuning")
{
  state.txt="cancelling"
  click luxView,1
  click luxStopPrint,0
}
thsp=SLEEP_TIME //Reset sleep time
printer.command.txt="M155 S5\rM106 S0\r" //Temp auto report, turn off fan
click sendCommand,1
page ReturnPage

///////////////////////////////////////////////////////////////////////////////
/// @fn luxView 1
/// @brief Updates states for controls
///////////////////////////////////////////////////////////////////////////////
if(state.txt=="init")
{
  oProgress.val=0
  tStatus.txt="Initializing"
}
if(state.txt=="cooling")
{
  tStatus.txt="Cooling hotends..."
}
if(state.txt=="tuning")
{
  tStatus.txt="Tuning"
  covx oProgress.val,tProgress.txt,0,0
  tProgress.txt+="%"
}
if(state.txt=="cancelling")
{
  tStatus.txt="Cancelling..."
}
if(state.txt=="complete")
{
  tStatus.txt="Complete"
}
add 2,0,printer.T0Temp.val //Plot temps
add 2,1,printer.T1Temp.val
add 2,2,printer.bedTemp.val
covx printer.T0Temp.val,vars.s.txt,0,0 //Update T0
tT0Temp.txt=vars.s.txt
covx printer.T1Temp.val,vars.s.txt,0,0 //Update T1
tT1Temp.txt=vars.s.txt
covx printer.bedTemp.val,vars.s.txt,0,0 //Update Bed
tBedTemp.txt=vars.s.txt

///////////////////////////////////////////////////////////////////////////////
/// @fn checkHotbed
/// @brief Hotbed must be cool
///////////////////////////////////////////////////////////////////////////////
if(printer.bedTemp.val>40) //Stop if heatbed is too hot
{
  diagConfirm.tMessage.txt="Hot bed must be below\r40C"
  click luxInit,0 //Cleanup
  page MAIN
}

///////////////////////////////////////////////////////////////////////////////
/// @fn checkHotends
/// @brief Hotends must be cool
///////////////////////////////////////////////////////////////////////////////
if(printer.T0Temp.val<40&&printer.T1Temp.val<40)
{
  state.txt="cooling"
}

///////////////////////////////////////////////////////////////////////////////
/// @fn tune
/// @brief Perfrom PID Autotune on hotends and bed
///////////////////////////////////////////////////////////////////////////////
printer.command.txt="M303 C10 E0 S220 U" //T0 PID Tune
click luxSend,1
printer.command.txt="M303 C10 E1 S220 U" //T1 PID Tune
click luxSend,1
printer.command.txt="M303 C5 -1 S60 U" //Bed PID Tune
click luxSend,1

///////////////////////////////////////////////////////////////////////////////
/// @fn finish
/// @brief Save the results and direct to confirmation
///////////////////////////////////////////////////////////////////////////////
printer.command.txt="M500" //Save Results
click luxSend,1
click luxInit,0
diagConfirm.tMessage.txt="PID Tuning Complete"
DiagReturnPage=ReturnPage
page diagConfirm