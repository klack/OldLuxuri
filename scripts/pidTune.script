//On Load
DiagReturnPage=dp
if(HandleCallback==1)
{
  if(ReturnObj==0) //Direct to controller callback
  {
    click controller,0
  }else
  {
    click b[ReturnObj],0
  }
  ReturnVal=-1
  HandleCallback=0
}
click controller,1
click updateState,1

//---------------------------------------------------------
//Controller
//States: null, error, init, cooling, ready, tuning, complete
//---------------------------------------------------------
if(vars.state.txt=="null")
{
  diagPrompt.tMessage.txt="Perform PID Autotune?\rThis will take some time."
  ReturnObj=0
  page diagPrompt
}
if(vars.state.txt=="init")
{
  oProgress.val=0
  if(printer.bedTemp.val>40) //Stop if heatbed is too hot
  {
    diagConfirm.tMessage.txt="Hot bed must be below\r40C"
    DiagReturnPage=4 //main
    page diagConfirm
  }
  prints "M106 S255",0 //Turn on fan
  printh 0A
  prints "M155 S1",0 //Temp auto report
  printh 0A
  delay=1000
  if(printer.T0Temp.val<40&&printer.T1Temp.val<40)
  {
    vars.state.txt="ready"
  }else
  {
    vars.state.txt="cooling"
    tStatus.txt="Cooling hotends"
  }
}

if(vars.state.txt=="cooling")
{
  if(printer.T0Temp.val<40&&printer.T1Temp.val<40)
  {
    vars.state.txt="ready"
  }  
}

if(vars.state.txt=="ready")
{
  vars.state.txt="tuning"
  tStatus.txt="Starting PID Autotune"
  prints "M303 C3 E0 S220 U",0 //T0 PID Tune
  printh 0A
  prints "M303 C3 E1 S220 U",0 //T1 PID Tune
  printh 0A
  prints "M303 C3 E-1 S60 U",0 //Bed PID Tune
  printh 0A
}

if(vars.state.txt=="tuning")
{
  oProgress.val=cycle.val*100/totalCycles.val
  covx oProgress.val,tStatus.txt,0,0
  tStatus.txt+="%"
  if(oProgress.val==100)
  {
    vars.state.txt="complete"
    tStatus.txt="Complete"
    prints "M500",0 //Save Results
    printh 0A
    prints "M155 S2",0 //Temp auto report
    printh 0A
    diagConfirm.tMessage.txt="PID Tuning Complete"
    DiagReturnPage=4
    page diagConfirm
  }
}
//---------------------------------------------------------
//End Controller
//---------------------------------------------------------

//Controller Callback
vars.state.txt="init"

//Update State
add 1,0,printer.T0Temp.val
add 1,1,printer.T1Temp.val
add 1,2,printer.bedTemp.val

//parsePIDTune
// bias: 109 d: 109 min: 215.00 max: 226.00
parse.lineMatch.txt=" bias: "
strlen parse.lineMatch.txt,j
substr parse.buffer.txt,parse.eval.txt,0,j
if(parse.eval.txt==parse.lineMatch.txt)
{
  cycle.val+=1
}

//destroy
prints "M108",0 //Cancel
printh 0A
delay=1000
prints "M108",0
printh 0A
delay=1000
prints "M108",0
printh 0A
delay=1000
prints "M108",0
printh 0A
delay=1000
prints "M108",0
printh 0A
delay=1000
prints "M108",0
printh 0A
delay=1000
prints "M155 S2",0 //Temp auto report
printh 0A
vars.state.txt="null"
HandleCallback=-1
ReturnVal=-1
ReturnObj=-1
page ReturnPage

//tmState
click updateState,1
click controller,1

//Close
click destroy,1
