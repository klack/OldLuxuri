//---------------------------------------------------------
//Controller
//States: null, error, init, heating, printing, paused, complete
//---------------------------------------------------------
if(printer.printState.txt=="null")
{
  printer.printState.txt="init"
}
if(printer.printState.txt=="init")
{
  tProgress.txt="Starting Print"
  printer.command.txt="M27 S4"  //Report SD Status
  click sendCommand,1
  printer.command.txt="M23 "
  printer.command.txt+=fileName.txt
  click sendCommand,1
  printer.command.txt="M24"
  click sendCommand,1
  printer.command.txt="M31"
  click sendCommand,1
  printer.printState.txt="heating"
}
if(printer.printState.txt=="heating")
{
  vars.debug.txt="here1"
  if(printer.T0SetTemp.val>0||printer.T1SetTemp.val>0) //Check if heaters are set
  {  
    vars.debug.txt="here2"
    if(printer.T0Temp.val>=printer.T0SetTemp.val&&printer.T1Temp.val>=printer.T1SetTemp.val) //Check if heaters are ready
    {
      vars.debug.txt="here3"
      if(ok==1&&heatCount.val>1) //Make sure data is being received
      {
        vars.debug.txt="here4"
        printer.printState.txt="printing"
      }
      heatCount.val+=1 //To handle multiple heaters ready
    }
  }
}
if(printer.printState.txt=="printing") //Printing
{
  if(ok==1) //Only get time if an ok was recieved
  {
    printer.command.txt="M31"
    click sendCommand,1
  }    
  if(printer.sdPrinting.val==0) //Handle a finished print while LCD was asleep
  {
    printer.printState.txt="finished"
  }
}
if(printer.printState.txt=="finished") //Finished
{
  printer.command.txt="M27 S0" //Turn off report sdcard status
  click sendCommand,1
  printer.flow.val=100   //Clear printer related printer vars
  printer.speed.val=100
  printer.printState.txt="null"
  diagConfirm.tMessage.txt="Print Complete"
  diagConfirm.tMessage.txt+="\r"
  diagConfirm.tMessage.txt+=printer.printTime.txt
  DiagReturnPage=4 //main
  page diagConfirm
}
if(printer.printState.txt=="error") //Error
{
  DiagReturnPage=4 //main
  diagConfirm.tMessage.txt="Error opening file"
  page diagConfirm
}
//---------------------------------------------------------
//End Controller
//---------------------------------------------------------

//---------------------------------------------------------
//updateView
//---------------------------------------------------------
covx printer.T0Temp.val,vars.s.txt,0,0 //Update T0
tT0Temp.txt=vars.s.txt
tT0Temp.txt+="/"
covx printer.T0SetTemp.val,vars.s.txt,0,0
tT0Temp.txt+=vars.s.txt
covx printer.T1Temp.val,vars.s.txt,0,0 //Update T1
tT1Temp.txt=vars.s.txt
tT1Temp.txt+="/"
covx printer.T1SetTemp.val,vars.s.txt,0,0 
tT1Temp.txt+=vars.s.txt
covx printer.bedTemp.val,vars.s.txt,0,0 //Update Bed
tBedTemp.txt=vars.s.txt
tBedTemp.txt+="/"
covx printer.setBedTemp.val,vars.s.txt,0,0 
tBedTemp.txt+=vars.s.txt
covx printer.fanSpeed.val,vars.s.txt,0,0 //Fan Speed
tFanSpeed.txt=vars.s.txt
tM117.txt=printer.m117.txt //M117
if(printer.printState.txt=="null")
{
  tProgress.txt=""
  oProgress.val=0
  tTime.aph=0
}
if(printer.printState.txt=="printing") 
{
  oProgress.val=printer.currentByte.val*100/printer.totalBytes.val
  covx oProgress.val,vars.s.txt,0,0
  tProgress.txt=vars.s.txt
  tProgress.txt+="%"
  tTime.aph=127
  if(printer.printTime.txt!="0s")
  {
    tTime.txt=printer.printTime.txt
  }
}
if(printer.printState.txt=="heating")
{
  tProgress.txt="Heating..."
  oProgress.val=0
  tTime.aph=0
}
//---------------------------------------------------------
//End updateView
//---------------------------------------------------------

//---------------------------------------------------------
//Pre-initialize
//---------------------------------------------------------
click updateView,1
ReturnPage=dp
DiagReturnPage=dp
if(HandleCallback==1)
{
  if(ReturnObj==0) //Direct to custom controller callback
  {
    click controller,0
  }else
  {
    click b[ReturnObj],0
  }
  ReturnVal=-1
  HandleCallback=0
}
click controller,1


//---------------------------------------------------------
//sendCommand
//---------------------------------------------------------
prints printer.command.txt,0
printh 0A
ok=0

// //Mock Data
// parse.buffer.txt="echo:Print time: 7d 7m 7s"
// click notifyEOL,1
// parse.buffer.txt="SD printing byte 20/100"
// click notifyEOL,1
// parse.buffer.txt="Done printing file"
// click notifyEOL,1