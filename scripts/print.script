//--------------------
//Update State
//--------------------
//Update T0
covx printer.T0Temp.val,vars.s.txt,0,0
tT0Temp.txt=vars.s.txt
tT0Temp.txt+="/"
covx printer.T0SetTemp.val,vars.s.txt,0,0
tT0Temp.txt+=vars.s.txt
//Update T1
covx printer.T1Temp.val,vars.s.txt,0,0
tT1Temp.txt=vars.s.txt
tT1Temp.txt+="/"
covx printer.T1SetTemp.val,vars.s.txt,0,0
tT1Temp.txt+=vars.s.txt
//Update Bed
covx printer.bedTemp.val,vars.s.txt,0,0
tBedTemp.txt=vars.s.txt
tBedTemp.txt+="/"
covx printer.setBedTemp.val,vars.s.txt,0,0
tBedTemp.txt+=vars.s.txt
//Fan Speed
covx printer.fanSpeed.val,vars.s.txt,0,0
tFanSpeed.txt=vars.s.txt
//Progress
if(printer.printState.val==-1) //Uninitialized
{
  tTime.aph=0
  oProgress.val=0
  tProgress.txt=""
}
if(printer.printState.val==0) //Starting
{
  tProgress.txt="Starting Print..."
}
if(printer.printState.val==1) //Printing
{
  if(printer.T0SetTemp.val>0||printer.T1SetTemp.val>0)
  {  
    j=0
    k=0
    l=0
    //Check T0Temp
    if(printer.T0Temp.val>=printer.T0SetTemp.val)
    {
      j=1
    }
    //Check T1Temp
    if(printer.T1Temp.val>=printer.T1SetTemp.val)
    {
      k=1
    }
    //Check Bed Temp
    if(printer.bedTemp.val>=printer.setBedTemp.val)
    {
      l=1
    }
    State=j+k+l
    if(State==3)
    {
      heatersReady.val=1
    }
  }

  if(heatersReady.val==1)
  {
    oProgress.val=printer.currentByte.val*100/printer.totalBytes.val
    covx oProgress.val,vars.s.txt,0,0
    tProgress.txt=vars.s.txt
    tProgress.txt+="%"
    tTime.aph=127
    tTime.txt=printer.printTime.txt
  }else
  {
    tProgress.txt="Heating..."
    tTime.aph=0
  }
}
//printer.printState.val==2 //Paused
if(printer.printState.val==3) //Finished
{
  printer.flow.val=-1   //Clear printer related printer vars
  printer.speed.val=-1
  printer.printState.val=-1
  prints "M27 S0",0  //Turn off report sdcard status
  printh 0A
  diagConfirm.tMessage.txt="Print Complete"
  diagConfirm.tMessage.txt+="\r"
  diagConfirm.tMessage.txt+=printer.printTime.txt
  DiagReturnPage=4 //main
  page diagConfirm
}
if(printer.printState.val==9) //Error
{
  tProgress.txt="Error opening file"
}
//--------------------
//End Update State
//--------------------

//On load
//Todo get long file name
ReturnPage=dp
DiagReturnPage=dp
if(printer.printState.val==-1)
{
  prints "M27 S4",0  //Report SD Status
  printh 0A
  prints "M23 ",0
  prints fileName.txt,0
  printh 0A
  prints "M24",0
  printh 0A
  prints "M31",0
  printh 0A
}
click updateState,1


//Get print time
if(printer.printState.val==1)
{
  if(ok==1) //Only print if an ok was recieved
  {
    ok=0
    prints "M31",0
    printh 0A
  }
}


//Mock Data
parse.buffer.txt="echo:Print time: 7d 7m 7s"
click notifyEOL,1
parse.buffer.txt="SD printing byte 20/100"
click notifyEOL,1
parse.buffer.txt="Done printing file"
click notifyEOL,1