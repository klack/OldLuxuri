///////////////////////////////////////////////////////////////////////////////
/// @fn controller,1
///
/// @brief Program loop
/// @brief state.txts: null, ready
///////////////////////////////////////////////////////////////////////////////
if(printer.printState.txt=="null")
{
  commands.command.txt="M220\r" 
  commands.command.txt="M221\r"
  click sendCommand,1
  printer.printState.txt="ready"
}
if(printer.printState.txt=="stopping")
{
  tsw 255,0
  if(ok==1)
  {
    printer.printState.txt="null"
    page main
  }
}

///////////////////////////////////////////////////////////////////////////////
/// @fn updateView,1
///
/// @brief Updates states for controls
///////////////////////////////////////////////////////////////////////////////
if(printer.printState.txt=="stopping") //Paused
{
  bStop.picc=bStop.picc2

}
if(printer.printState.txt=="paused") //Paused
{
  bPlayPause.picc=bPlayPause.picc2+2
  tPlayPause.txt="Resume"
  setlayer bClose,0
  bClose.aph=0
}
if(printer.speed.val!=100&&printer.speed.val!=0) //Speed
{
  covx printer.speed.val,bSpeed.txt,0,0
  bSpeed.picc=bSpeed.picc2+2
}else
{
  bSpeed.txt=""
  bSpeed.picc=bSpeed.picc2+1
}
if(printer.flow.val!=100&&printer.flow.val!=0) //Flow
{
  covx printer.flow.val,bFlow.txt,0,0
  bFlow.picc=bFlow.picc2+2
}else
{
  bFlow.txt=""
  bFlow.picc=bFlow.picc2+1
}
if(printer.printState.txt=="printing") //Printing
{
  bPlayPause.picc=bPlayPause.picc2+1
  tPlayPause.txt="Pause"
  setlayer bClose,255
  bClose.aph=127
}



///////////////////////////////////////////////////////////////////////////////
/// @fn Pre-initialize
/// @brief Page Initialization
///////////////////////////////////////////////////////////////////////////////
click updateView,1
ReturnPage=dp
DiagReturnPage=dp
if(HandleCallback==1)
{
  if(ReturnObj==0) //Direct to custom controller callback
  {
    click controller,0
  }else
  {
    click b[ReturnObj],0
  }
  ReturnVal=-1
  HandleCallback=0
}
click controller,1


///////////////////////////////////////////////////////////////////////////////
//bSpeed click
ReturnObj=2
numpad.tCaption.txt="Print Speed"
if(printer.speed.val==0)
{
  vars.returnVal.txt=""
}else
{
  covx printer.speed.val,vars.returnVal.txt,0,0
}
page numpad
//bSpeed callback
covx printer.speed.val,vars.s.txt,0,0
if(vars.returnVal.txt!=vars.s.txt)
{
  commands.queue.txt+="M220 S"
  commands.queue.txt+=vars.returnVal.txt
  commands.queue.txt+="\r"
  commands.queue.txt+="M220\r"
  click sendCommand,0
}

//bFlow click
ReturnObj=3
numpad.tCaption.txt="Print Flow"
if(printer.flow.val==0)
{
  vars.returnVal.txt=""
}else
{
  covx printer.flow.val,vars.returnVal.txt,0,0
}
page numpad
//bFlow callback
covx printer.flow.val,vars.s.txt,0,0
if(vars.returnVal.txt!=vars.s.txt)
{
  commands.queue.txt+="M221 S"
  commands.queue.txt+=vars.returnVal.txt
  commands.queue.txt+="\r"
  commands.queue.txt+="M221\r"
  click sendCommand,0
}

//bPlayPause click
if(printer.printState.txt=="printing"||printer.printState.txt=="heating")
{
  commands.queue.txt+="M25" //Pause print
  commands.queue.txt+="G91" //Move out the way
  commands.queue.txt+="G0 X-5 Y-5 Z10"
  commands.queue.txt+="G90"
  printer.printState.txt="paused"
}else
{
  commands.queue.txt+="G91" //Move back
  commands.queue.txt+="G0 X5 Y5 Z-10"
  commands.queue.txt+="G90"
  commands.queue.txt+="M24" //Resume print
  printer.printState.txt="printing"
}
click updateView,1

//Stop click
diagPrompt.tMessage.txt="Stop Print?"
ReturnObj=4
page diagPrompt
//Stop callback
if(ReturnVal!=-1)
{
  commands.command.txt="M108\r"
  click sendCommand,1
  commands.command.txt="M108\r"
  click sendCommand,1
  commands.command.txt="M108\r"
  click sendCommand,1
  commands.command.txt="M108\r"
  click sendCommand,1  
  commands.command.txt="M524\r"
  click sendCommand,1
  delay=1000
  commands.command.txt=commands.stopPrint.txt
  click sendCommand,1
  printer.flow.val=100   //Clear printer related printer vars
  printer.speed.val=100
  printer.printState.txt="stopping"
  click updateView,1
}

///////////////////////////////////////////////////////////////////////////////
/// @fn run
/// @brief Program Loop on 1000ms timer
///////////////////////////////////////////////////////////////////////////////
click updateView,1
click controller,1
click sendCommand,0


// //Mock Data
// parse.buffer.txt="echo:E1 Flow: 90%"
// click notifyEOL,1
// parse.buffer.txt="FR:90%"
// click notifyEOL,1