//Update State
if(printer.speed.val!=100&&printer.speed.val!=0)
{
  covx printer.speed.val,bSpeed.txt,0,0
  bSpeed.picc=39
}else
{
  bSpeed.txt=""
  bSpeed.picc=38
}
if(printer.flow.val!=100&&printer.flow.val!=0)
{
  covx printer.flow.val,bFlow.txt,0,0
  bFlow.picc=39
}else
{
  bFlow.txt=""
  bFlow.picc=38
}
if(printer.printState.txt=="printing") //Printing
{
  bPlayPause.picc=38
  bPlayPause.picc2=37
  tPlayPause.txt="Pause"
  setlayer bClose,255
  bClose.aph=127
}
if(printer.printState.txt=="paused") //Paused
{
  bPlayPause.picc=39
  bPlayPause.picc2=37
  tPlayPause.txt="Resume"
  setlayer bClose,0
  bClose.aph=0
}

//Onload
DiagReturnPage=dp
click updateState,1
prints "M220",0
printh 0A
prints "M221",0
printh 0A
if(HandleCallback==1)
{
  HandleCallback=0
  click b[ReturnObj],0 //Run right click code as callback
  vars.returnVal.txt="null"
  ReturnVal=-1
}

//bSpeed click
ReturnObj=13
numpad.tCaption.txt="Print Speed"
if(printer.speed.val==0)
{
  vars.returnVal.txt=""
}else
{
  covx printer.speed.val,vars.returnVal.txt,0,0
}
page numpad
//bSpeed callback
covx printer.speed.val,vars.s.txt,0,0
if(vars.returnVal.txt!=vars.s.txt)
{
  prints "M220 S",0
  prints vars.returnVal.txt,0
  printh 0A
  prints "M220",0
  printh 0A
}

//bFlow click
ReturnObj=14
numpad.tCaption.txt="Print Flow"
if(printer.flow.val==0)
{
  vars.returnVal.txt=""
}else
{
  covx printer.flow.val,vars.returnVal.txt,0,0
}
page numpad
//bFlow callback
covx printer.flow.val,vars.s.txt,0,0
if(vars.returnVal.txt!=vars.s.txt)
{
  prints "M221 S",0
  prints vars.returnVal.txt,0
  printh 0A
  prints "M221",0
  printh 0A
}

//bPlayPause click
if(printer.printState.txt=="printing"||printer.printState.txt=="heating")
{
  prints "M25",0 //Pause print
  printh 0A
  prints "G91",0 //Move out the way
  printh 0A
  prints "G0 X-5 Y-5 Z10",0
  printh 0A
  prints "G90",0
  printh 0A
  printer.printState.txt="paused"
}else
{
  prints "G91",0 //Move back
  printh 0A
  prints "G0 X5 Y5 Z-10",0 
  printh 0A
  prints "G90",0
  printh 0A
  prints "M24",0 //Resume print
  printh 0A
  printer.printState.txt="printing"
}

//Stop click
diagPrompt.tMessage.txt="Stop Print?"
ReturnObj=16
ReturnVal=1
page diagPrompt
//Stop callback
if(ReturnVal!=-1)
{
  commands.queue.txt=commands.stopPrint.txt
  click sendCommand,0
  printer.printState.txt="null"
  printer.flow.val=100   //Clear printer related printer vars
  printer.speed.val=100
  page main
}



// //Mock Data
// parse.buffer.txt="echo:E1 Flow: 90%"
// click notifyEOL,1
// parse.buffer.txt="FR:90%"
// click notifyEOL,1